[Connection의 OpenSchema, RecordSet의 NextRecordSet]


이번에 소개할 것은 두가지 인데요. 하나는 SQL 서버의 스토어드 프로시져를 사용하지 않고도 지정한 디비의
테이블들의 목록을 뽑아오는 방법이구요.. 또 하나는 ASP 페이지에서 여러개의 레코드셋을 동시에 열어야할 경우
속도를 빠르게 하는 방법에 대한 이야기랍니다.

1.Connection 객체의 OpenSchema 메소드

OpenSchema 메소드는 현재 사용하는 디비의 스키마를 읽어올 수 있게 하는 메소듭니다.
그렇기에, 테이블의 목록을 읽어올 수도 있는 것이지요. 저의 경우는 전에는 스토어드 
프로시져를 통해서 디비의 테이블 목록을 가져오고는 했었습니다... 
이 방법을 사용하는 것이 좋아보여서 소개하고자 합니다. 일단, 다음과 같은 소스를 코딩하시구 결과를 봐주시기 바랍니다.

[OpenSchema.asp]

<!--METADATA TYPE="typelib" FILE="C:\program Files\Common Files\System\ado\msado15.dll"-->
<%
Dim Dbcon 
Dim Rs 
Dim strConnect 
strConnect = "Provider=sqloledb;Data Source=(local); Initial catalog=taeyo; User ID=sa; Password="

Set Dbcon = Server.CreateObject("ADODB.Connection") 
Dbcon.Open strConnect

Set Rs = Dbcon.OpenSchema(adSchemaTables) 
Do Until Rs.EOF 
  if Trim(Rs("Table_Type")) = "TABLE" then 
     Response.Write "테이블 타입 : " & Rs("Table_Type") & "  테이블 명 : " & Rs("Table_name") & "<br>"
  End if 
  Rs.MoveNext 
Loop

Rs.Close 
Set Rs = Nothing 
Dbcon.Close 
Set Dbcon = Nothing 
%>


그리고, 결과를 확인하시면 현재의 디비의 테이블의 목록을 보실수가 있습니다.

위에서는 OpenSchema 메소의 인자로 adSchematables 라는 상수를 사용했기에 테이블관련 데이터들을 불러올 수가 있었는데요.
OpenSchma 메소드로 가져올 수 있는 정보는 이외에도 대단히 많습니다. 그리고, 그에 따라 이 상수도 다른 것으로 지정해야 하지요...
이것의 목록은 대단히 많아서요... 이 강좌에는 다 올리기기 버거울 것 같네요... 

Provider를 통해서, 디비의 스키마 정보를 얻어내는 메소드... 이 메소드는 어느 곳에 사용할 수 있을까요? 아마도 그 빈도는
잦지 않겠지만, 분명 사용할 만한 것이 있을 것입니다. 저의 경우는 동아리(커뮤니티)서비스를 만들거나, 조금은 확장된
대화방을 만들때 뭐 그럴때 조금은 유용하더군요... 뭐 사용하는 사람에 따라 틀릴 수 있기는 하지만요~~


============================================


2. RecordSet 객체의 NextRecordSet 메소드

이번에 소개할 메소드는 RecoedSet 객체의 메소드로써, 하나의 ASP 페이지에서 여러개의 테이블을 각각 열수 밖에 없는 경우
사용할 만한 메소드 입니다. 각각의 테이블을 열고 그 데이터를 담기 위해서는 각각의 레코드셋이 필요합니다만...
그럴 경우, 이 메소드를 사용하는 것을 함 고려해 보세요...

이 메소드가 하는 역할이 무엇이냐구요? 이것은 다중의 레코드셋 객체를 만들게 해주는 메소드이지요...

[NextRecordSet.asp]

<!--METADATA TYPE="typelib" FILE="C:\program Files\Common Files\System\ado\msado15.dll"-->
<%
Dim Dbcon 
Dim Rs 
Dim strConnect 
strConnect = "Provider=sqloledb;Data Source=(local); Initial catalog=pubs; User ID=sa; Password=" 
Set Dbcon = Server.CreateObject("ADODB.Connection") 
Dbcon.Open strConnect

Set Rs = Server.CreateObject("ADODB.RecordSet") 
sql = "Select * from stores; Select * from jobs" 
Rs.Open sql,Dbcon, , ,adCmdText

i = 1 
Do Until Rs is Nothing 
     if i =1 then Response.write "<b>Stores 테이블의 데이터들을 담은 레코드셋 : </b><br>" 
     if i = 2 then Response.write "<br><b>Jobs 테이블의 데이터들을 담은 레코드셋 :</b><br>" 
     Do While Not Rs.EOF 
          Response.Write "   " & Rs(0) & " , " &rs(1) & " , " &rs(2) & "<br>" 
          Rs.MoveNext 
     Loop 
     Set Rs = Rs.NextRecordset 
     i = i + 1 
Loop

Set Rs = Nothing 
Dbcon.Close 
Set Dbcon = Nothing 
%>